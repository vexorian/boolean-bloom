title Boolean Bloom 1.42
author vexorian
homepage vexorian.com
(require_player_movement)
again_interval 0.1
run_rules_on_level_start
(debug
verbose_logging)

enable_level_select

========
OBJECTS
========

Background
#446E14 #497715
00000
01000
00000
00010
00000

Background2
black

Wall
#D6920A #926407
01000
11111
00010
00010
11111

PlayerFront
#291F30 #FF8E65 #8488D3 #8DDB34
.000.
.111.
02220
03230
.0.0.

PlayerFrontSkip1
#291F30 #FF8E65 #8488D3 #8DDB34
.000.
.111.
02220
03230
.0.0.

PlayerFrontSkip0
#291F30 #FF8E65 #8488D3 #8DDB34
.000.
.111.
02220
03230
.0.0.



PlayerRight
#291F30 #FF8E65 #8488D3 #8DDB34
.000.
.011.
02220
02230
.0.0.

PlayerLeft
#291F30 #FF8E65  #8488D3 #8DDB34
.000.
.110.
02220
03220
.0.0.


Goal
#C93600 #A12B00
01010
1...1
0...0
1...1
01010

Button
#CFD3C1
0...0
.0.0.
..0..
.0.0.
0...0

Block
#A9AC9D #CFD3C1 #878A7E #3C2E47
.111.
10103
11023
10203
.333.


Door
#574368
.....
.0.0.
.....
.0.0.
.....

ClosedDoor
#3C2E47
.0.0.
00000
.0.0.
00000
.0.0.


NegaDoor
Blue White
11111
11001
11111
10011
11111

ClosedNegaDoor
Blue Black
11111
10011
11111
11001
11111

Trap
#574368
.....
.0.0.
.....
.0.0.
.....

ClosedTrap
#3C2E47
.0.0.
00000
.0.0.
00000
.0.0.


NegaTrap
Blue
.....
.0...
.0.0.
...0.
.....

ClosedNegaTrap
Blue Black
11111
11101
10101
10111
11111

PlantHeadRight
#ED3F00 #8ddb34 #f8c868 Pink #3C2E47 #3C2E47 ( #FF6026 )
.55..
5005.
13204
4004.
.44..

PlantHeadRightOff
#ED3F00 #8ddb34 #f8c868 Pink #3C2E47 #3C2E47 ( #FF6026 )
.55..
5005.
13204
4004.
.44..


PlantHeadLeft
#ED3F00 #8ddb34 #f8c868 Pink #3C2E47 #3C2E47 ( #FF6026 )
..55.
.5005
40231
.4004
..44.

PlantHeadLeftOff
#ED3F00 #8ddb34 #f8c868 Pink #3C2E47 #3C2E47 ( #FF6026 )
..55.
.5005
40231
.4004
..44.



PlantHeadUp
#ED3F00 #8ddb34 #f8c868 Pink #3C2E47 #3C2E47 ( #FF6026 )
..5..
.505.
50204
50304
.414.

PlantHeadUpOff
#ED3F00 #8ddb34 #f8c868 Pink #3C2E47 #3C2E47 ( #FF6026 )
..5..
.505.
50204
50304
.414.

PlantHeadDown
#ED3F00 #8ddb34 #f8c868 Pink #3C2E47 #3C2E47 ( #FF6026 )
.514.
50304
50204
.504.
..4..

PlantHeadDownOff
#ED3F00 #8ddb34 #f8c868 Pink #3C2E47 #3C2E47 ( #FF6026 )
.514.
50304
50204
.504.
..4..



PlantHorizontal
#6AA427 #3C2E47 #8ddb34
..2..
22022
00000
10111
.1...

PlantVertical
#6AA427 #3C2E47 #8ddb34
.201.
2001.
.201.
.2001
.201.

GoalTic0
transparent

GoalTic1
transparent

OpenMarker
transparent

WallBottomBorder
#3C2E47
.....
.....
.....
.....
00000

WallRightBorder
#3C2E47
....0
....0
....0
....0
....0

WallTopBorder
#f8c868 
00000
.....
.....
.....
.....

WallLeftBorder
#f8c868 
0....
0....
0....
0....
0....

U0
#A4AF95 #B1BBA1
.....
.....
.....
.001.
.0.0.

D0
#A4AF95 #B1BBA1
.0.0.
.1.0.
.000.
.....
.....

U1
#A4AF95 #B1BBA1
.....
.....
.....
..0..
.00..

D1
#A4AF95 #B1BBA1
..0..
..0..
.000.
.....
.....

U2
#A4AF95 #B1BBA1
.....
.....
.....
.001.
...0.

D2
#A4AF95 #B1BBA1
.000.
.1...
.000.
.....
.....

D3
#A4AF95 #B1BBA1
.000.
...0.
.000.
.....
.....

U4
#A4AF95 #B1BBA1
.....
.....
.....
.0.1.
.0.0.

D4
#A4AF95 #B1BBA1
.000.
...0.
...0.
.....
.....

U5
#A4AF95 #B1BBA1
.....
.....
.....
.001.
.0...

D6
#A4AF95 #B1BBA1
.000.
.1.0.
.000.
.....
.....


D7
#A4AF95 #B1BBA1
...0.
...0.
...0.
.....
.....


=======
LEGEND
=======
P = PlayerFront and Background
# = Wall 
. = Background
: = Background2
X = Button and Background
o = Block and Background
D = Door and Background
N = NegaDoor
T = Trap and Background
M = NegaTrap
> = PlantHeadRight and Background
< = PlantHeadLeft and Background
v = PlantHeadDown and Background
^ = PlantHeadUp and Background

@ = Goal and Background
% = Goal and Block and Background

⨅ = u0 and Background
▟ = u1 and Background
┐ = u2 and Background
॥ = u4 and Background
┏ = u5 and Background


⨆ = d0 and Background
┻ = d1 and Background
ⵎ = d2 and Background
⊃ = d3 and Background
┓ = d4 and Background
⸋ = d6 and Background
┃ = d7 and Background

Player = PlayerFront or PlayerRight or PlayerLeft or PlayerFrontSkip1 or PlayerFrontSkip0
WithCollision = Player or Wall or Block or PlantHeadRight or PlantHorizontal or PlantVertical or PlantHeadRightOff or PlantHeadLeft or PlantHeadLeftOff or PlantHeadUp or PlantHeadUpOff or PlantHeadDown or PlantHeadDownOff
Closed = ClosedDoor or ClosedNegaDoor or ClosedTrap or ClosedNegaTrap
DoorOrTrap = Door or NegaDoor or Trap or NegaTrap
WithCollisionOrClosed = WithCollision or Closed

Flower = PlantHeadRight or PlantHeadRightOff or PlantHeadLeft or PlantHeadLeftOff or PlantHeadUp or PlantHeadUpOff or PlantHeadDown or PlantHeadDownOff

Pusher = Player or Block
Heavy = Player or Block or Flower or PlantHorizontal or PlantVertical

=======
SOUNDS
=======

SFX0 5276507 (growth)
SFX1 13577907 (push plant)
SFX2 6565307 (push rock)
SFX3 73560700 (goal)
SFX4 9898706 (ungoal)
SFX5 80397904 (door)
SFX6 64824904 (no door)

================
COLLISIONLAYERS
================

Background
Background2
u0,d0,u1,u2,d1,d2,d2,d3,u4,d4,u5,d6,d7
Button
Door, NegaDoor, Trap, NegaTrap
WithCollisionOrClosed 
Goal

GoalTic0
GoalTic1
OpenMarker


WallLeftBorder
WallBottomBorder
WallRightBorder
WallTopBorder

======
RULES
======

left [ > Player no PlayerLeft  ] -> [ > PlayerLeft ]
right [ > Player no PlayerRight ] -> [ > PlayerRight ]
vertical [ > Player no PlayerFront ] -> [ > PlayerFront ]


[ > Pusher | Block ] -> [ > Pusher | > Block ] SFX2
left [ > Pusher | PlantHeadRight | PlantHorizontal ] -> [  | Pusher | PlantHeadRight ] SFX1
right [ > Pusher | PlantHeadLeft | PlantHorizontal ] -> [  | Pusher | PlantHeadLeft ] SFX1
up [ > Pusher | PlantHeadDown | PlantVertical ] -> [  | Pusher | PlantHeadDown ] SFX1
down [ > Pusher | PlantHeadUp | PlantVertical ] -> [  | Pusher | PlantHeadUp ] SFX1


( Nega-door logic :)
late [ ClosedNegaDoor] -> []
late [ NegaDoor No WithCollision ] -> [ NegaDoor ClosedNegaDoor ]
late [ ClosedNegaDoor ][ Button No Heavy ] -> [][ Button ]

( Door logic :)
late [ ClosedDoor ] -> []
late [ Door No ClosedDoor No WithCollision ][ Button No Heavy ] -> [ Door ClosedDoor ][ Button ]

( Trap logic :)
late [ ClosedTrap ] -> []
late [ Trap No ClosedTrap No WithCollision ][ Button Heavy ] -> [ Trap ClosedTrap][ Button Heavy ]

( Nega-Trap logic:  )
late [ ClosedNegaTrap ] -> []
late [ NegaTrap No WithCollision ] -> [ NegaTrap ClosedNegaTrap ]
late [ ClosedNegaTrap ][ Button Heavy ] -> [  ][ Button Heavy ]


( Door SFX)
late [ OpenMarker Closed ] -> [ Closed ] sfx5
late [ DoorOrTrap No Closed No OpenMarker] -> [ DoorOrTrap OpenMarker ] sfx6

(Some tweeks to plant growth race conditions so that they are smarter)

( Prioritize plant entering goal: )

late right [ PlantHeadRight No Goal | Goal No WithCollisionOrClosed ] -> [ PlantHorizontal | PlantHeadRightOff Goal ]
late left [ PlantHeadLeft No Goal | Goal No WithCollisionOrClosed ] -> [ PlantHorizontal | PlantHeadLeftOff Goal ]
late up [ PlantHeadUp No Goal | Goal No WithCollisionOrClosed ] -> [ PlantVertical | PlantHeadUpOff Goal ]
late down [ PlantHeadDown No Goal | Goal No WithCollisionOrClosed ] -> [ PlantVertical | PlantHeadDownOff Goal ]




(Plant growth)

late right [ PlantHeadRight |  No WithCollisionOrClosed ] -> [ PlantHorizontal | PlantHeadRightOff ]
late [ PlantHeadRightOff ] -> [ PlantHeadRight ] again SFX0

late left [ PlantHeadLeft |  No WithCollisionOrClosed ] -> [ PlantHorizontal | PlantHeadLeftOff ]
late [ PlantHeadLeftOff ] -> [ PlantHeadLeft ] again SFX0

late up [ PlantHeadUp |  No WithCollisionOrClosed ] -> [ PlantVertical | PlantHeadUpOff ]
late [ PlantHeadUpOff ] -> [ PlantHeadUp ] again SFX0

late down [ PlantHeadDown |  No WithCollisionOrClosed ] -> [ PlantVertical | PlantHeadDownOff ]
late [ PlantHeadDownOff ] -> [ PlantHeadDown ] again SFX0



(Goals)

late [ GoalTic0 No Flower ] -> [  ]
late [ GoalTic1 No Flower ] -> [  ] SFX4


late [ GoalTic0 Goal Flower No GoalTic1 ] -> [ GoalTic0 GoalTic1 Goal Flower ] SFX3
late [ Goal Flower ] -> [ GoalTic0 Goal Flower ] again

(Decorating walls)
down [ wall | no wall ] -> [ wall WallBottomBorder | ]
right [ wall | no wall ] -> [ wall WallRightBorder | ]
left [ wall | no wall ] -> [ wall WallLeftBorder | ]
up [ wall | no wall ] -> [ wall WallTopBorder | ]

up       [ Background |      ] -> [ action Background  | ]
[ stationary Background Wall ] -> [ Wall Background WallTopBorder ]
[ action Background ] -> [ Background ]

down     [ Background |      ] -> [ action Background  | ]
[ stationary Background Wall ] -> [ Wall Background WallBottomBorder ]
[ action Background ] -> [ Background ]

left     [ Background |      ] -> [ action Background  | ]
[ stationary Background Wall ] -> [ Wall Background WallLeftBorder ]
[ action Background ] -> [ Background ]

right    [ Background |      ] -> [ action Background  | ]
[ stationary Background Wall ] -> [ Wall Background WallRightBorder ]
[ action Background ] -> [ Background ]


==============
WINCONDITIONS
==============

All Goal on GoalTic1

=======
LEVELS
=======

(::::::::::::::::::::::
:###################@:
:######.............#:
:######....t........#:
:#............t.....#:
:#.....#>....<#.....#:
:#.........t....tt..#:
:#>..o.o.op..........#:
:#..................#:
:#........###########:
:#x..^....Do......@<#:
:####################:
::::::::::::::::::::::)



Message Welcome Gardener
Message Please make sure all flowers stay in the assigned locations.
Message Use direction keys to move, press U to undo and R to restart.
Message Press escape to return to level select if you want to skip a level.

level_select_point (01)
############
#..........#
#>...oo.@..#
#....oo....#
#..........#
#>@P.o...⨅▟#
#........⨆┻#
############

level_select_point (02)

##################
########.........#
#....###....P.#..#
#>...D.D@.#.ooo..#
#....###....#x#⨅┐#
########.......⨆ⵎ#
##################


level_select_point (03)
############
#⨅┐....v...#
#⨆⊃........#
#..#@..o..<#
#..........#
#......P.@.#
#...###....#
#...vv#.#^.#
#.#.ooo.####
#...@@.#####
############

level_select_point (04)
#################
########...#..v.#
########...T..o.#
##.........#.o@##
##.........#...##
###.####.P.######
#>..oo.D......@.#
###..#.D....#.⨅॥#
####...#..X.#.⨆┓#
#################




(::::::::::::::::::::
:##################:
:#>...@...d.......#:
:###########......#:
:#................#:
:#>.@@...oo..<##px#:
:#............##.x#:
:#............##..#:
:##################:
::::::::::::::::::::)

level_select_point (05)
##################
########.#########
#>......o@.......#
########.##.P....#
#.....@......o..<#
#>.@@...oo..<##..#
#⨅┏..........##..#
#⨆⊃..........##@.#
###############^##
##################


level_select_point (06)
##################
#⨅┏........##..@##
#⨆⸋...#.#..v....##
#>..o##....o..#.##
#.....##....##.o##
#......o.P.......#
##X#...........^##
##X#...^..#D######
##X###.#.##.######
#####..o..#.######
#######^###@######
##################


level_select_point (07)
###############
#.........v.⨅┐#
#......P....⨆┃#
#.%<#.........#
#.%<#.....o..<#
##.##..o....@.#
##.##>.o.@....#
##.##@.o<#....#
##@##....#..^.#
##^####.#######
###############


level_select_point (08)
#######################
###...o...#############
#....#.#.##############
#>.o@#^#..v.#...⨅⨅#.o.#
#....###..o.#.P.⨆⸋#...#
###>.D.D@...T.....D...#
#....###..o.T.....#.@.#
########..@.#.#x#.#.^.#
#######################


(
::::::::::::::::::::
:##################:
:#.P........##..@##:
:#.....#.#..v....##:
:#>..o##....o..#.##:
:#.....##....##.o##:
:#>o.@..o.........#:
:#>o.@......@...^##:
:#>o.@..^..########:
:##.###.#.#########:
:##.##..o..########:
:##.####^##########:
:##################:
::::::::::::::::::::
)


level_select_point (09)
####################
#T.....@........D.<#
#X#...@.......⨅⨅D.<#
#T...@........⨆⊃D.<#
###>..oo....@..<####
#>...o..o......<####
#>..o..@.o.....<####
#>.o..P...o....<####
####################

Message Well done!

